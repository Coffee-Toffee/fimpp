Dear Princess Celestia: Letter about Discord.

I learned about Applejack with Pinkie Pie:
    By the way, "+".
    On page numbered by Pinkie Pie of Sweetie Belle I read about Twilight, because she is always a temporary variable.
    When Twilight was nothing:
        Did you know Twilight is zero?
    That's what I did!
    Twilight got one more.
    When Twilight had not less than Tom:
        Did you know Twilight is zero?
    That's what I did!
    On page numbered by Pinkie Pie of Sweetie Belle I scribbled about Twilight.
That's about Applejack.

I learned about Liarjack with Pinkie Pie:
    By the way, "-".
    On page numbered by Pinkie Pie of Sweetie Belle I read about Twilight.
    When either Twilight was nothing or Twilight was equal to zero:
        Did you know Twilight is Tom?
    That's what I did!
    Twilight got one fewer.
    When Twilight had less than zero:
        Twilight did sum of Tom and Twilight!
    That's what I did!
    On page numbered by Pinkie Pie of Sweetie Belle I scribbled about Twilight.
That's about Liarjack.

I learned about Rainbow Dash with Pinkie Pie:
    By the way, ">".
    Pinkie Pie got one more.
That's about Rainbow Dash with Pinkie Pie.

I learned about Rainbow Crash with Pinkie Pie:
    By the way, "<".
    Pinkie Pie got one less.
That's about Rainbow Crash with Pinkie Pie.

I learned about Spike with Pinkie Pie:
    On page numbered by Pinkie Pie of Sweetie Belle I read about Twilight.
    When Twilight was nothing:
        Did you know Twilight is zero?
    That's what I did!
    Celestia did character by code of Twilight.
    I quickly said: Celestia!
That's about Spike.

I learned about Nightmare Moon with Pinkie Pie and Rarity:
    By the way, "[".
    On page numbered by Pinkie Pie of Sweetie Belle I read about Twilight.
    When either Twilight is nothing or Twilight is equal to zero:
        Did you know Angel is one, because he's gonna hold the bracket number?
        Rarity got one more!
        I did this while Angel had more than zero:
            On page numbered by Rarity of Discord I read about Derpy.
            When Derpy was equal to "[":
                Angel got one more.
            That's what I did!
            When Derpy was equal to "]":
                Angel got one fewer, because sucks to be him!
            That's what I did!
            Rarity got one more.
        That's what I did!
    That's what I did!
That's about Nightmare Moon with Rarity.

I learned about Luna with Pinkie Pie and Rarity:
    By the way, "]".
    On page numbered by Pinkie Pie of Sweetie Belle I read about Twilight.
    When Twilight is somepony and Twilight has more than zero:
        By the way, null checks are important.
        Angel made difference of zero and one, because he's gonna hold the bracket number.
        Rarity got one less!
        I did this while Angel had less than zero:
            On page numbered by Rarity of Discord I read about Derpy.
            When Derpy was equal to "[":
                Angel got one more.
            That's what I did!
            When Derpy was equal to "]":
                Angel got one fewer, because sucks to be him!
            That's what I did!
            Rarity got one less.
        That's what I did!
    That's what I did!
That's about Luna with Rarity.

Today I learned about Turing completeness.

    Yes, that Tom. Did you know Tom likes the number 256?

    Yes, I mean that Discord. Discord did letters of
    "++++++++[>++++[>++>+++>+++>+<<<<-]>+>->+>>+[<]<-]>>.>>---.+++++++..+++.>.<<-.>.+++.------.--------.>+.>++.+++." .
    Yes, that Sweetie Belle. Today I found a book named Sweetie Belle, because she is the memory.

    Did you know Pinkie Pie likes the number one, because she is the memory pointer?
    Did you know Rarity likes Pinkie Pie, because she is the instruction pointer?
    On the first page of Discord I read about Derpy, because she will keep the current instruction.
    I did this while Derpy was somepony:
        When Derpy was equal to "+":
            I made Applejack of Pinkie Pie!
        That's what I did!
        When Derpy was equal to "-":
            I made Liarjack of Pinkie Pie!
        That's what I did!
        When Derpy was equal to ">":
            Pinkie Pie made Rainbow Dash of Pinkie Pie!
        That's what I did!
        When Derpy was equal to "<":
            Pinkie Pie made Rainbow Crash of Pinkie Pie!
        That's what I did!
        When Derpy was equal to ".":
            I did Spike of Pinkie Pie.
        That's what I did!
        When Derpy was equal to "[":
            Rarity did Nightmare Moon of Pinkie Pie and Rarity.
        That's what I did!
        When Derpy was equal to "]":
            Rarity did Luna of Pinkie Pie and Rarity.
        That's what I did!
        Rarity got one more.
        On the page numbered by Rarity of Discord I read about Derpy.
    That's what I did!

    I quickly sang new line, because why not.

Your faithful student, Flutterjerk.